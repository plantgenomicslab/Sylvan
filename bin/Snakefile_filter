import yaml, sys, os, re
import pandas as pd
from pathlib import Path

# TODO: file handling for re-runs is inadequate ... if a new gff is used many outputs need to be overwritten

# Results directory prefix - all outputs will be placed under this directory
RESULTS_DIR = "results/"

# Load config file (can be overridden via SYLVAN_FILTER_CONFIG environment variable)
config_file_path = os.environ.get('SYLVAN_FILTER_CONFIG', 'config_filter.yml')
with open(config_file_path, 'r') as config_file:
	try:
		config_dict=yaml.safe_load(config_file)
	except yaml.YAMLError as e:
		sys.exit("Could not load configs! Check config.yaml ...")

# Check inputs
for input in config_dict["Input"]:
	if input == "chrom_regex":
		continue
	if input == "busco_lin":
		continue
	if input == "HmmDB":
		if config_dict["Input"][input] == "/usr/local/src/Pfam-A.hmm":
			continue
		elif not os.path.exists(config_dict["Input"][input]):
			sys.exit(f"Could not find {config_dict['Input'][input]}! Check {input} in config.yaml ...")
	if not os.path.exists(config_dict["Input"][input]):
		sys.exit(f"Could not find {config_dict['Input'][input]}! Check {input} in config.yaml ...")

image = config_dict["Singularity"]

# Get rnaseq run wildcards
rna_seq = config_dict["Input"]["rna_seq"]
bams = []
rsem = []
# Support both _1/_2 and _R1/_R2 naming conventions for paired-end reads
pair1_re = re.compile(r"(.+)(_1|_R1)\.fastq\.gz$")
pair2_re = re.compile(r"(.+)(_2|_R2)\.fastq\.gz$")
# Store mapping of sample names to actual file paths
sample_to_files = {}
for root, dirs, files in os.walk(rna_seq):
	for fi in files:
		match1 = pair1_re.match(fi)
		if match1:
			sample_name = match1.group(1)
			bams.append(f"{RESULTS_DIR}FILTER/STAR/{sample_name}.STAR_pairedend.bamAligned.toTranscriptome.out.bam")
			rsem.append(f"{RESULTS_DIR}FILTER/rsem_outdir/{sample_name}.isoforms.results")
			if sample_name not in sample_to_files:
				sample_to_files[sample_name] = {}
			sample_to_files[sample_name]['fwd'] = os.path.join(root, fi)

		match2 = pair2_re.match(fi)
		if match2:
			sample_name = match2.group(1)
			if sample_name not in sample_to_files:
				sample_to_files[sample_name] = {}
			sample_to_files[sample_name]['rev'] = os.path.join(root, fi)

		# Single-end support...
		#elif se_re.match(fi):
		#	os.system(f"ln -sf {os.path.join(root,fi)} GETA/fastp/single/{fi}")
		#	bams.append(f"GETA/HiSat2/single/hisat2.{basename.group(1)}.bam")
	break

def get_fastq_fwd(wildcards):
	return sample_to_files[wildcards.run]['fwd']

def get_fastq_rev(wildcards):
	return sample_to_files[wildcards.run]['rev']

pep_string = re.sub(r"\.fa.*$", "", config_dict["Input"]["anot_gff"] + ".fa")
pep_string = re.sub(r"^.*\/", "", pep_string)

os.makedirs(f"{RESULTS_DIR}logs", exist_ok=True)
os.makedirs(f"{RESULTS_DIR}FILTER/rsem_outdir", exist_ok=True)

def inputAll(wc):
	input = [f"{RESULTS_DIR}FILTER/pfam.out",
		f"{RESULTS_DIR}FILTER/BLASTP.OUT.TMP",
		f"{RESULTS_DIR}FILTER/rsem_outdir/RSEM.isoforms.results",
		f"{RESULTS_DIR}FILTER/STAR/merged.bam",
		f"{RESULTS_DIR}FILTER/cov.bed",
		f"{RESULTS_DIR}FILTER/homolog.pdb",
		f"{RESULTS_DIR}FILTER/augustus_coverage.bed",
		f"{RESULTS_DIR}FILTER/helixer_coverage.bed",
		f"{RESULTS_DIR}FILTER/repeat_coverage.bed",
		f"{RESULTS_DIR}FILTER/filter.gff3",
		f"{RESULTS_DIR}FILTER/{pep_string}.cdna",
		f"{RESULTS_DIR}FILTER/{pep_string}.pep"]
		#f"{RESULTS_DIR}FILTER/busco_eudicots_odb10/run_eudicots_odb10/full_table.tsv"]
	for i in range(1,21):
		input.append(f"{RESULTS_DIR}FILTER/splitPep/{pep_string}.part_{i:03d}.pep")
		input.append(f"{RESULTS_DIR}FILTER/splitPep/{pep_string}.part_{i:03d}.blast.tmp")

	return(input)

anot_cdna = f"{RESULTS_DIR}FILTER/{pep_string}.cdna"
anot_pep = f"{RESULTS_DIR}FILTER/{pep_string}.pep"

rule all:
	input: inputAll

rule makeCDSPep:
	output: 
		cdna = anot_cdna,
		pep = anot_pep
	params:
		genome = config_dict["Input"]["genome"],
		gff =  config_dict["Input"]["anot_gff"]
	singularity: image
	shell:"""
		gff3_file_to_proteins.pl {params.gff} {params.genome} cDNA > {output.cdna}

		gff3_file_to_proteins.pl {params.gff} {params.genome} prot > {output.pep}
		"""

rule PfamScan:
	input: anot_pep
	output: f"{RESULTS_DIR}FILTER/pfam.out"
	threads: config_dict["Threads"]["PfamScan"]
	params:
		hmm = config_dict['Input']['HmmDB'],
		singularity = image
	shell:"""
		singularity exec {params.singularity} \
		pfam_scan.pl \
			-fasta {input} \
			-dir {params.hmm} \
			-cpu {threads} \
			-outfile {output}
		"""

# Prepare RSEM files
rule prepRSEM:
	input: anot_cdna
	output: f"{anot_cdna}.seq"
	singularity: image
	threads: config_dict["Threads"]["prepRSEM"]
	params: cdna = anot_cdna
	shell: """
		rsem-prepare-reference \
		-p {threads} \
		{input} \
		{params.cdna}
		"""

rule fastp_PAIRED:
	input:
		fwd = get_fastq_fwd,
		rev = get_fastq_rev
	output:
		fwd = f"{RESULTS_DIR}GETA/fastp/paired/{{run}}_1.fq.gz",
		rev = f"{RESULTS_DIR}GETA/fastp/paired/{{run}}_2.fq.gz"
	singularity: image
	threads: config_dict["Threads"]["fastp_PAIRED"]
	params:
		results_dir = RESULTS_DIR
	shell:"""
		fastp --detect_adapter_for_pe \
			--overrepresentation_analysis \
			--cut_right \
			--thread {threads} \
			--json {params.results_dir}GETA/fastp/paired/{wildcards.run}.fastp.json \
			--html {params.results_dir}GETA/fastp/paired/{wildcards.run}.fastp.html \
			-i {input.fwd} -I  {input.rev} \
			-o {output.fwd} -O {output.rev}
		"""

rule STAR_index:
	output: temp(f"{RESULTS_DIR}FILTER/STAR/SA")
	threads: config_dict["Threads"]["STAR_index"]
	singularity: image
	params:
		g = config_dict["Input"]["genome"],
		gff = config_dict["Input"]["anot_gff"],
		results_dir = RESULTS_DIR
	shell:"""
		STAR \
		--runThreadN {threads} \
		--runMode genomeGenerate \
		--genomeDir {params.results_dir}FILTER/STAR \
		--genomeFastaFiles {params.g} \
		--genomeSAindexNbases 12 \
		--sjdbGTFfile {params.gff} \
		--sjdbGTFtagExonParentTranscript Parent \
		--outTmpDir {params.results_dir}FILTER/STAR/_STARtmp_index
		"""

rule STAR_paired:
	input:
		g  = f"{RESULTS_DIR}FILTER/STAR/SA",
		one = f"{RESULTS_DIR}GETA/fastp/paired/{{run}}_1.fq.gz",
		two = f"{RESULTS_DIR}GETA/fastp/paired/{{run}}_2.fq.gz",
	output: temp(f"{RESULTS_DIR}FILTER/STAR/{{run}}.STAR_pairedend.bamAligned.toTranscriptome.out.bam")
	threads: config_dict["Threads"]["STAR"]
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell: """
			STAR --runMode alignReads \
			--runThreadN {threads} \
			--outFilterMultimapNmax 10 \
			--alignIntronMin 25 \
			--alignIntronMax 25000 \
			--genomeDir {params.results_dir}FILTER/STAR \
			--outSAMtype BAM SortedByCoordinate \
			--limitBAMsortRAM 216000000000 \
			--outBAMsortingBinsN 200 \
			--outSAMattributes XS \
			--outSAMstrandField intronMotif \
			--outFileNamePrefix {params.results_dir}FILTER/STAR/{wildcards.run}.STAR_pairedend.bam \
			--outTmpDir {params.results_dir}FILTER/STAR/{wildcards.run}_STARtmp \
			--readFilesCommand gunzip -c \
			--readFilesIn {input.one} {input.two} \
			--quantMode TranscriptomeSAM \
			--quantTranscriptomeBan IndelSoftclipSingleend \
			--alignEndsType EndToEnd
			"""

rule calculateRSEMExpression_PAIRED:
	input:
		prep = f"{anot_cdna}.seq",
		bam = f"{RESULTS_DIR}FILTER/STAR/{{run}}.STAR_pairedend.bamAligned.toTranscriptome.out.bam"
	output: f"{RESULTS_DIR}FILTER/rsem_outdir/{{run}}.isoforms.results"
	threads: config_dict["Threads"]["calculateRSEMExpression"]
	singularity: image
	params:
		cdna = anot_cdna,
		results_dir = RESULTS_DIR
	shell:"""
		rsem-calculate-expression \
		--no-bam-output -p {threads} \
		--alignments \
		--paired-end {input.bam} {params.cdna} {params.results_dir}FILTER/rsem_outdir/{wildcards.run}
		"""

rule mergeRSEM:
	input: rsem
	output: f"{RESULTS_DIR}FILTER/rsem_outdir/RSEM.isoforms.results"
	run:
		num_inputs = len(input)
		summary = pd.read_csv(input.pop(0), sep="\t", usecols=["transcript_id", "TPM"])
		
		for result in input:
			try:
				current = pd.read_csv(result, sep="\t", usecols=["transcript_id", "TPM"])
				summary["TPM"] = summary["TPM"] + current["TPM"]
			except:
				print(f"WARNING: Error parsing {result}")
				num_inputs -= 1
		
		summary["TPM"] = summary["TPM"]/num_inputs
		summary.to_csv(output[0], sep="\t", index=False, header=True)

rule mergeSTAR:
	input: bams
	output: temp(f"{RESULTS_DIR}FILTER/STAR/merged.bam")
	singularity: image
	threads: config_dict["Threads"]["mergeSTAR"]
	shell:"""
			samtools merge -f \
			--threads {threads} \
			{output} \
			{input}
		"""

rule sortSTAR:
	input: f"{RESULTS_DIR}FILTER/STAR/merged.bam"
	output: temp(f"{RESULTS_DIR}FILTER/STAR/sorted.bam")
	threads: config_dict["Threads"]["sortSTAR"]
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell:"""
		samtools sort \
		-@ {threads} \
		-o {params.results_dir}FILTER/STAR/sorted.bam \
		{input}
		"""

rule coverage:
	input: f"{RESULTS_DIR}FILTER/STAR/sorted.bam"
	output: f"{RESULTS_DIR}FILTER/cov.bed"
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell:"""
		samtools view -H {input} | \
		grep '@SQ' | \
		cut -f2,3 | \
		sed 's/[SL]N://g' \
		> {params.results_dir}FILTER/transcriptome_chromSizes.txt

		awk '{{print $1 \"\\t1\\t\" $2}}' {params.results_dir}FILTER/transcriptome_chromSizes.txt > {params.results_dir}FILTER/gene.bed

		bedtools coverage \
		-sorted \
		-g {params.results_dir}FILTER/transcriptome_chromSizes.txt \
		-a {params.results_dir}FILTER/gene.bed \
		-b {input} \
		> {output}
		"""
		
		

rule blastDB:
	input: config_dict["Input"]["protein"]
	output: f"{RESULTS_DIR}FILTER/homolog.pdb"
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell: "makeblastdb -in {input} -dbtype prot -out {params.results_dir}FILTER/homolog"

rule blastRexDB:
	input: config_dict['Input']['RexDB']
	output: f"{RESULTS_DIR}FILTER/rexdb.pdb"
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell: "makeblastdb -in {input} -dbtype prot -out {params.results_dir}FILTER/rexdb"

num = []
for i in range(1,21):
        num.append(f"{i:03d}")

rule splitPep:
	input: anot_pep
	output: expand(f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{number}.pep", number=num)
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell: "seqkit split -p 20 -O {params.results_dir}FILTER/splitPep {input}"

rule blast:
	input:
		wait = f"{RESULTS_DIR}FILTER/homolog.pdb",
		fa = f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{part}.pep"
	output:
		f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{part}.blast.tmp"
	threads: config_dict["Threads"]["blast"]
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell:"""blastp \
			-query {input.fa} \
			-db {params.results_dir}FILTER/homolog \
			-out {output} \
			-evalue 1e-15 \
			-outfmt '6 qseqid sseqid pident length mismatch gapopen qstart qend sstart send evalue bitscore qcovs' \
			-num_threads {threads} \
			-num_alignments 1
		"""

rule combineBlast:
	input: expand(f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{number}.blast.tmp", number=num)
	output: f"{RESULTS_DIR}FILTER/BLASTP.OUT.TMP"
	params:
		results_dir = RESULTS_DIR
	shell: "cat {params.results_dir}FILTER/splitPep/" + pep_string + ".part_*.blast.tmp > {output}"
		#| awk '{{if ($13 > " + str(config_dict["Cutoff"]["blast"]) + "){{print $0}}}}' > {output}"

rule blastRex:
	input:
		wait = f"{RESULTS_DIR}FILTER/rexdb.pdb",
		fa = f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{part}.pep"
	output:
		f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{part}.blastRex.tmp"
	threads: config_dict["Threads"]["blast"]
	singularity: image
	params:
		results_dir = RESULTS_DIR
	shell:"""blastp \
			-query {input.fa} \
			-db {params.results_dir}FILTER/rexdb \
			-out {output} \
			-evalue 1e-15 \
			-outfmt '6 qseqid sseqid pident length mismatch gapopen qstart qend sstart send evalue bitscore qcovs' \
			-num_threads {threads} \
			-num_alignments 1
		"""

rule combineBlastRex:
	input: expand(f"{RESULTS_DIR}FILTER/splitPep/" + pep_string + ".part_{number}.blastRex.tmp", number=num)
	output: f"{RESULTS_DIR}FILTER/BLASTP.OUT.Rex"
	params:
		results_dir = RESULTS_DIR
	shell: "cat {params.results_dir}FILTER/splitPep/" + pep_string + ".part_*.blastRex.tmp > {output}" 
		#| awk '{{if ($13 > " + str(config_dict["Cutoff"]["blastRex"])+ "){{print $0}}}}' > {output}"

#TODO: Add repeat coverage calculation... add to config as well
rule abInitioCoverage:
	output:
		f"{RESULTS_DIR}FILTER/augustus_coverage.bed",
		f"{RESULTS_DIR}FILTER/helixer_coverage.bed",
		f"{RESULTS_DIR}FILTER/repeat_coverage.bed"
	singularity: image
	params:
		augustus = config_dict["Input"]["augustus_gff"],
		helixer = config_dict["Input"]["helixer_gff"],
		repeat = config_dict["Input"]["repeat_gff"],
		gff = config_dict['Input']['anot_gff'],
		results_dir = RESULTS_DIR
	shell:"""
		grep -P '\tmRNA\t' {params.augustus} | \
		   cut -f1,4,5,7 > {params.results_dir}FILTER/augustus.bed

		grep -P '\tmRNA\t' {params.helixer} | \
		   cut -f1,4,5,7 > {params.results_dir}FILTER/helixer.bed

		cut -f1,4,5,7 {params.repeat} > {params.results_dir}FILTER/repeat.bed

		grep -P '\\tmRNA\\t' {params.gff} | \
			cut -f1,4,5,7,9 | \
			awk -F';' '{{print $1}}'| \
			sed 's/ID=//' | \
			sed 's/;.*//' > {params.results_dir}FILTER/prefilter.bed

		bedtools coverage -a {params.results_dir}FILTER/prefilter.bed -b {params.results_dir}FILTER/augustus.bed > {params.results_dir}FILTER/augustus_coverage.bed
		bedtools coverage -a {params.results_dir}FILTER/prefilter.bed -b {params.results_dir}FILTER/helixer.bed > {params.results_dir}FILTER/helixer_coverage.bed
		bedtools coverage -a {params.results_dir}FILTER/prefilter.bed -b {params.results_dir}FILTER/repeat.bed > {params.results_dir}FILTER/repeat_coverage.bed
		"""

rule lncRNA_pred:
	input: anot_cdna
	output: f"{RESULTS_DIR}FILTER/lncrna_predict.csv"
	params: s = image
	threads: config_dict["Threads"]["lncRNA_pred"]
	shell:'''
		singularity exec {params.s} \
		conda run -n lncdc \
		python /usr/local/src/LncDC-1.3.5/bin/lncDC.py \
		-x /usr/local/share/lncdc_db/plant_selected_nostructure_hexamer_table.csv \
		-m /usr/local/share/lncdc_db/plant_selected_nostructure_xgb_model_SIF_PF.pkl \
		-p /usr/local/share/lncdc_db/plant_selected_nostructure_imputer_SIF_PF.pkl \
		-s /usr/local/share/lncdc_db/plant_selected_nostructure_scaler_SIF_PF.pkl \
		-t 24 \
		-i {input} \
		-o {output}
		'''

rule busco:
	input: rules.makeCDSPep.output.pep
	output: f"{RESULTS_DIR}FILTER/busco_{config_dict['Input']['busco_lin']}/run_{config_dict['Input']['busco_lin']}/full_table.tsv"
	params:
		busco_lin = config_dict["Input"]["busco_lin"],
		results_dir = RESULTS_DIR
	singularity: image
	shell:"""
		busco -i {input} \
		-l {params.busco_lin} \
		-o {params.results_dir}FILTER/busco_{params.busco_lin} \
		-m protein \
		-f
		"""

rule filter:
	input:
		blast = f"{RESULTS_DIR}FILTER/BLASTP.OUT.TMP",
		rex = f"{RESULTS_DIR}FILTER/BLASTP.OUT.Rex",
		pfam = f"{RESULTS_DIR}FILTER/pfam.out",
		rsem = f"{RESULTS_DIR}FILTER/rsem_outdir/RSEM.isoforms.results",
		augustus = f"{RESULTS_DIR}FILTER/augustus_coverage.bed",
		helixer = f"{RESULTS_DIR}FILTER/helixer_coverage.bed",
		repeat = f"{RESULTS_DIR}FILTER/repeat_coverage.bed",
		cov = f"{RESULTS_DIR}FILTER/cov.bed",
		lncrna = f"{RESULTS_DIR}FILTER/lncrna_predict.csv",
		busco = f"{RESULTS_DIR}FILTER/busco_{config_dict['Input']['busco_lin']}/run_{config_dict['Input']['busco_lin']}/full_table.tsv"
	output:
		f"{RESULTS_DIR}FILTER/filter.gff3"
	params:
		anot_gff = config_dict["Input"]["anot_gff"],
		tpm = config_dict["Cutoff"]["tpm"],
		rsem_cov = config_dict["Cutoff"]["rsem_cov"],
		helixer_cov = config_dict["Cutoff"]["helixer_cov"],
		augustus_cov = config_dict["Cutoff"]["augustus_cov"],
		repeat_cov = config_dict["Cutoff"]["repeat_cov"],
		blast_pident = config_dict["Cutoff"]["blast_pident"],
		blast_qcovs = config_dict["Cutoff"]["blast_qcovs"],
		rex_pident = config_dict["Cutoff"]["rex_pident"],
		rex_qcovs = config_dict["Cutoff"]["rex_qcovs"],
		regex = config_dict["Input"]["chrom_regex"],
		results_dir = RESULTS_DIR
	singularity: image
	shell:"""
		python bin/Filter.py \
		{params.anot_gff} \
		{input.busco} \
		--tpm {params.tpm} \
		--rcov {params.rsem_cov} \
		--hcov {params.helixer_cov} \
		--acov {params.augustus_cov} \
		--repcov {params.repeat_cov} \
		--blast-pident {params.blast_pident} \
		--blast-qcovs {params.blast_qcovs} \
		--rex-pident {params.rex_pident} \
		--rex-qcovs {params.rex_qcovs} \
		--seed 123 \
		--trees 100 \
		--predictors 5 \
		--recycle 0.95 \
		--max-iter 10 \
		--chromRegex '{params.regex}' \
		--output {output} \
		--output-dir {params.results_dir}FILTER
	"""
